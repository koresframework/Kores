group 'com.koresframework'

apply plugin: 'kotlin'
apply plugin: 'license'

apply plugin: 'maven-publish'

jar {
    manifest {
        attributes 'Implementation-Title': 'Kores-DSL',
                'Implementation-Version': project.version
    }
}

repositories {
    mavenCentral()
    /*maven {
        url "https://maven.pkg.github.com/jonathanxd/jwiutils"
        credentials {
            username = project.findProperty("USERNAME") ?: System.getenv("USERNAME")
            password = project.findProperty("TOKEN") ?: System.getenv("TOKEN")
        }
    }*/
    maven {
        name "gitlab-maven"
        url "https://gitlab.com/api/v4/projects/28895078/packages/maven"
    }
    maven {
        name "gitlab-generic-parser"
        url "https://gitlab.com/api/v4/projects/29701822/packages/maven"
    }
}

dependencies {
    implementation rootProject
    testImplementation group: 'junit', name:'junit', version: '4.11'
}

compileKotlin {
    kotlinOptions.jvmTarget = "16"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "16"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

license {
    ext.name = 'KoresAPI-DSL'
    ext.organization = project.organization
    ext.description = 'Defines Kotlin DSL to create CodeAPI parts.'
    ext.url = project.purl
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.email = 'jonathan.scripter@programmer.net'

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header file('../LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}


publishing {
    repositories {
        maven {
            name = "Local"
            // change to point to your repo, e.g. http://my.org/repo
            url = "$buildDir/repo"
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/koresframework/kores"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
        maven {
            name = "GitLab"
            url "https://gitlab.com/api/v4/projects/28894889/packages/maven"
            credentials(HttpHeaderCredentials) {
                def ciToken = System.getenv("CI_JOB_TOKEN")
                if (ciToken != null && !ciToken.isEmpty()) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = project.findProperty("GITLAB_TOKEN") ?: System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
        maven {
            name = "GitLabJgang"
            url "https://gitlab.com/api/v4/projects/30392813/packages/maven"
            credentials(HttpHeaderCredentials) {
                def ciToken = System.getenv("CI_JOB_TOKEN")
                if (ciToken != null && !ciToken.isEmpty()) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = project.findProperty("GITLAB_TOKEN") ?: System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.kotlin
            artifactId 'kores-dsl'

            artifact sourcesJar
        }
    }
}